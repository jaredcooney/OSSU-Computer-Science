;; Design Quiz 4: Naturals and Helpers
;; How to Code: Simple Data (University of British Columbia)

;; Student: Jared Cooney
;; jaredcooney2@gmail.com

;; Runs in DrRacket (Beginning Student Language)

;; ======================================================================
(require 2htdp/image)

;; ===============================
;; Constants
(define COOKIES (circle 10 "solid" "brown"))

;; ===============================
;; Data Definitions

;; Natural is one of:
;;  - 0
;;  - (add1 Natural)
;; interp. a natural number
(define N0 0)         ;0
(define N1 (add1 N0)) ;1
(define N2 (add1 N1)) ;2

#;
(define (fn-for-natural n)
  (cond [(zero? n) (...)]
        [else
         (... n   ; n is added because it's often useful                   
              (fn-for-natural (sub1 n)))]))

;; Template rules used:
;;  - one-of: two cases
;;  - atomic distinct: 0
;;  - compound: 2 fields
;;  - self-reference: (sub1 n) is Natural


;; ======================================================================

;;PROBLEM 1:
;; Complete the design of a function called pyramid that takes a natural
;; number n and an image, and constructs an n-tall, n-wide pyramid of
;; copies of that image.


;; Natural Image -> Image
;; produce an n-wide pyramid of the given image
(check-expect (pyramid 0 COOKIES) empty-image)
(check-expect (pyramid 1 COOKIES) COOKIES)
(check-expect (pyramid 3 COOKIES)
              (above COOKIES
                     (beside COOKIES COOKIES)
                     (beside COOKIES COOKIES COOKIES)))

;(define (pyramid n i) empty-image) ; stub
;; <template from Natural>

(define (pyramid n i)
  (cond [(zero? n) empty-image]
        [else
         (above           
          (pyramid (sub1 n) i)
          (image-row n i))]))




;; Natural Image -> Image
;; produce a row of n copies of image i
(check-expect (image-row 0 COOKIES) empty-image)
(check-expect (image-row 1 COOKIES) COOKIES)
(check-expect (image-row 3 COOKIES) (beside COOKIES COOKIES COOKIES))

;(define (image-row n i) i)  ;stub
;; <template from Natural>

(define (image-row n i)
  (cond [(zero? n) empty-image]
        [else
         (beside i            
                 (image-row (sub1 n) i))]))


;; ======================================================================

;; Problem 2:
;;
;; Consider a test tube filled with solid blobs and bubbles.  Over time the
;; solids sink to the bottom of the test tube, and as a consequence the bubbles
;; percolate to the top.  Let's capture this idea in BSL.
;;
;; Complete the design of a function that takes a list of blobs and sinks each
;; solid blob by one. It's okay to assume that a solid blob sinks past any
;; neighbor just below it.
;;
;; To assist you, we supply the relevant data definitions.


;; Blob is one of:
;; - "solid"
;; - "bubble"
;; interp.  a gelatinous blob, either a solid or a bubble
;; Examples are redundant for enumerations
#;
(define (fn-for-blob b)
  (cond [(string=? b "solid") (...)]
        [(string=? b "bubble") (...)]))

;; Template rules used:
;; - one-of: 2 cases
;; - atomic distinct: "solid"
;; - atomic distinct: "bubble"



;; ListOfBlob is one of:
;; - empty
;; - (cons Blob ListOfBlob)
;; interp. a sequence of blobs in a test tube, listed from top to bottom.
(define LOB0 empty) ; empty test tube
(define LOB2 (cons "solid" (cons "bubble" empty))) ; solid blob above a bubble

#;
(define (fn-for-lob lob)
  (cond [(empty? lob) (...)]
        [else
         (... (fn-for-blob (first lob))
              (fn-for-lob (rest lob)))]))

;; Template rules used
;; - one-of: 2 cases
;; - atomic distinct: empty
;; - compound: 2 fields
;; - reference: (first lob) is Blob
;; - self-reference: (rest lob) is ListOfBlob



;; ListOfBlob -> ListOfBlob
;; produce a list of blobs that sinks the given solid blobs by one

(check-expect (sink empty) empty)
(check-expect (sink (cons "bubble" (cons "solid" (cons "bubble" empty))))
              (cons "bubble" (cons "bubble" (cons "solid" empty))))
(check-expect (sink (cons "solid" (cons "solid" (cons "bubble" empty))))
              (cons "bubble" (cons "solid" (cons "solid" empty))))
(check-expect (sink (cons "solid" (cons "bubble" (cons "bubble" empty))))
              (cons "bubble" (cons "solid" (cons "bubble" empty))))
(check-expect (sink (cons "solid" (cons "bubble" (cons "solid" empty))))
              (cons "bubble" (cons "solid" (cons "solid" empty))))
(check-expect (sink (cons "bubble" (cons "solid" (cons "solid" empty))))
              (cons "bubble" (cons "solid" (cons "solid" empty))))
(check-expect (sink (cons "solid"
                          (cons "solid"
                                (cons "bubble" (cons "bubble" empty)))))
              (cons "bubble" (cons "solid" 
                                   (cons "solid" (cons "bubble" empty)))))
;(define (sink lob) empty) ; stub

(define (sink lob)
  (cond [(empty? lob) empty]
        [else
         (sink-top (first lob)
                   (sink (rest lob)))]))



;; Blob ListOfBlob -> ListOfBlob
;; iff Blob b is "solid", flips b with the first item in lob and returns the resulting list
(check-expect (sink-top "bubble" empty) (cons "bubble" empty))
(check-expect (sink-top "solid" empty) (cons "solid" empty))
(check-expect (sink-top "bubble" (cons "solid" (cons "solid" empty)))
              (cons "bubble" (cons "solid" (cons "solid" empty))))
(check-expect (sink-top "solid" (cons "bubble" (cons "solid" empty)))
              (cons "bubble" (cons "solid" (cons "solid" empty))))
(check-expect (sink-top "bubble" (cons "solid" (cons "bubble" empty)))
              (cons "bubble" (cons "solid" (cons "bubble" empty))))

;(define (sink-top b lob) (cons b lob))  ;stub
;; <template from ListOfBlob>

(define (sink-top b lob)
  (cond [(empty? lob) (cons b empty)]
        [else
         (if (is-solid? b)
             (cons (first lob) (cons b (rest lob)))
             (cons b lob))]))



;; Blob -> Boolean
;; produce true if Blob b is "solid" and false if b is "bubble"
(check-expect (is-solid? "solid")  true)
(check-expect (is-solid? "bubble") false)

;(define (is-solid? b) false)  ;stub
;; <template from Blob>

(define (is-solid? b)
  (cond [(string=? b "solid")  true]
        [(string=? b "bubble") false]))

